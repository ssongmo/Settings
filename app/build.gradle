//모듈의 용도 : application, library
apply plugin: 'com.android.application'

//안드로이드 앱관련 설정
android {
    compileSdkVersion 25 //컴파일(class 파일 생성)러 버전
    buildToolsVersion "25.0.1" //빌드(설치를 위한 apk 생성) 툴 버전
    defaultConfig {
        applicationId "com.songmoo.anroid.settings" //앱의 고유 ID, 보통 앱의 root package명을 사용.
        minSdkVersion 15 //지원하는 최하위 Android 버전(API Level)
        targetSdkVersion 25 // 컴파일과 빌드의 목표가 되는 버전.
        versionCode 1 //내부적인 관리버전(정수)
        versionName "1.0" //외부적으로 알려지는 앱 버전
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner" //테스트환경
    }
    lintOptions {
        abortOnError false
    }
    signingConfigs{
        release{
            storeFile file("../keystore/keystore.jks")
            storePassword "123456"
            keyAlias "testKey"
            keyPassword "123456"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            buildConfigField("String", "MYURL", "\"배포용 버전\"");
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug{

                buildConfigField("String", "MYURL", "\"개발용 버전\"");

        }
    }
    productFlavors{
        google{
            versionName "1.0-google"
        }
        onestore{
            versionName "1.0-onstore"
        }
        samsung{
            versionName "1.0-samsung"
        }
    }


}
//라이브러리
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
//하위 버전 호환성을 위한 라이브러리
    compile 'com.android.support:appcompat-v7:25.0.1'
//화번 테스트용 라이브러리
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
//소스코드 테스트용 라이브러리
    testCompile 'junit:junit:4.12'
}
